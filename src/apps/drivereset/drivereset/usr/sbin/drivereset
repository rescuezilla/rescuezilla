#!/usr/bin/perl -w
# ----------------------------------------------------------------------
#   Drive Reset
#   Resets a hard drive to factory settings.  For more information, see:
#     * http://ata.wiki.kernel.org/index.php/ATA_Secure_Erase
#     * http://cmrr.ucsd.edu/people/Hughes/SecureErase.shtml
# ----------------------------------------------------------------------
#   Copyright (C) 2012 RedoBackup.org
# ----------------------------------------------------------------------
#   This program is free software: you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation, either version 3 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this program.  If not, see <http://www.gnu.org/licenses/>.
# ----------------------------------------------------------------------

use strict;
use warnings;
use Data::Dumper;
use Glib qw{ TRUE FALSE };
use Gtk2 '-init';
use Gtk2::SimpleList;
use Fcntl qw(F_GETFL F_SETFL O_NONBLOCK);
use POSIX qw(locale_h);
use locale;
use utf8;

# Load translation
use constant TRANSLATION_DIR => "/usr/share/drivereset/translations-drivereset/";
my $lc_messages = setlocale(LC_MESSAGES);
# Extract the ISO 639-1 code
my $lang_code = substr($lc_messages,0,2);
print "LC_MESSAGES locale:             $lc_messages.\n";
print "Attempting to load translation: ".TRANSLATION_DIR."$lang_code.ko\n\n";

# Exports 'loc'
use Locale::Maketext::Simple (
    Path => TRANSLATION_DIR,
    Style => 'gettext',
);

# Sets language to the LC_MESSAGES environment variable
loc_lang($lang_code);

##### Constants found in our program ###################################
use constant TEST_MODE     => FALSE;
use constant DATA_DIR      => "/usr/share/drivereset/";
use constant GLADE_UI      => "drivereset.glade";
my @RESET_STEPS   =  ( loc("Warning!"),
                       loc("Step 1: Select Target Drives"),
                       loc("Step 2: Confirm Drive Reset"),
                       loc("Step 3: Factory Reset Drives") );
my $msg = "";

##### Main code ########################################################
main();
exit(0);


##### Functions and Subroutines ########################################
sub main {
  # Start the main application
  our $builder;
  my $window;
  $builder = Gtk2::Builder->new();
  # Load translations to strings in the Glade file.
  print("Setting GTK translation domain to load files from eg, /usr/share/locale/".$lang_code."/LC_MESSAGES/drivereset.mo\n");
  $builder->set_translation_domain("drivereset");
  $builder->add_from_file(DATA_DIR.GLADE_UI);
  $window = $builder->get_object('main_app');
  $builder->connect_signals(undef);
  $builder->get_object('main_tabs')->set_show_tabs(FALSE);
  set_subtitle($RESET_STEPS[0]);
  $window->show();
  Gtk2->main();
}

sub reset_step {
  # Advance to the next step
  my $step = $_[0];
  if (!defined($step)) { $step = 1; }
  our $builder;
  $step = $builder->get_object('main_tabs')->get_current_page()+1;
  set_subtitle($RESET_STEPS[$step-1]);
  if ($step == 1) {
    # Reset Step 1
    $builder->get_object('next_button')->set_sensitive(TRUE);
  } elsif ($step == 2) {
    # Reset Step 2
    $builder->get_object('next_button')->set_sensitive(FALSE);
    set_drive_list('target_drives');
    $builder->get_object('btnRefresh')->grab_focus();
  } elsif ($step == 3) {
    # Reset Step 3
    $builder->get_object('next_button')->set_sensitive(TRUE);
  } elsif ($step == 4) {
    # Reset Step 4
    refresh_window();
    $builder->get_object('next_button')->set_sensitive(FALSE);
    $builder->get_object('next_button')->hide();
    our %PROGRESS = ();
    prepare_all();
    reset_all();
  }
  refresh_window();
}

sub prepare_all {
  # Loop through the targeted drives and prepare each
  our %PROGRESS;
  our %drives;
  our @targets;
  $PROGRESS{'done'} = FALSE;
  our $builder->get_object('progress_bar')->set_sensitive(TRUE);
  foreach my $target (@targets) {
    $PROGRESS{$target}{'done'} = FALSE;
    $PROGRESS{$target}{'description'} = $drives{$target}{'dev_desc'}." (".$drives{$target}{'dev_size'}.")";
    $msg = loc("Preparing to reset device %1", $target);
    update_status($target, $msg);
    prepare_drive($target);
    refresh_window();
  }
  refresh_window();
}

sub reset_all {
  # Loop through the targeted drives and start resetting each
  our %PROGRESS;
  our %drives;
  our @targets;
  foreach my $target (@targets) {
    $msg = loc("Starting to reset device %1",$target);
    update_status($target, $msg);
    reset_drive($target);
    refresh_window();
  }
  Glib::Timeout->add(100, \&update_progress);
  refresh_window();
}

sub prepare_drive {
  # Prepare a drive for reset
  our %PROGRESS;
  my $target = $_[0];
  # Verify that no partitions on this device are mounted
  $msg = loc("Making sure drive is not mounted");
  update_status($target, $msg);
  print "*** umount /dev/$target*\n";
  `umount /dev/$target*`;
  my $mounts = `mount | grep /dev/$target | wc -l`;
  if ($mounts > 0) {
    beeper('warning');
    $msg = loc("There are %1 partitions still mounted on device %2. This drive will not be reset.", $mounts, $target);
    message_box($msg, 'error');
    $PROGRESS{$target}{'done'} = TRUE;
    $PROGRESS{$target}{'error'} = TRUE;
    $msg = loc("Unable to reset drive: Disk is mounted");
    update_status($target, $msg);
    return 0;
  }
  # Get the drive's current status
  $msg = loc("Getting current status of device %1", $target);
  update_status($target, $msg);
  print "*** udevadm trigger\n";
  `udevadm trigger`;
  sleep(3);
  my $details = get_drive_details($target);
  # Does drive support secure erase commands?
  $msg = loc("Verifying ATA secure erase support for device %1", $target);
  update_status($target, $msg);
  if ($$details{'id_ata_feature_set_security'} ne '1') {
    beeper('warning');
    $msg = loc("Device %1 does not support the ATA secure erase command. This drive will not be reset.", $target);
    message_box($msg, 'error');
    $PROGRESS{$target}{'done'} = TRUE;
    $PROGRESS{$target}{'error'} = TRUE;
    $msg = loc("Unable to reset drive: ATA secure erase not supported");
    update_status($target, $msg);
    return 0;
  }
  # Is drive frozen?
  $msg = loc("Making sure drive is not frozen");
  update_status($target, $msg);
  if ($$details{'id_ata_feature_set_security_frozen'} eq '1') {
    beeper('warning');
    $msg = loc("Device %1 is still marked as frozen. This drive will not be reset.",$target);
    message_box($msg, 'error');
    $PROGRESS{$target}{'done'} = TRUE;
    $PROGRESS{$target}{'error'} = TRUE;
    $msg = loc("Unable to reset drive: Disk marked frozen");
    update_status($target, $msg);
    return 0;
  }
  # Is drive locked or password set?
  $msg = loc("Making sure drive is not locked");
  update_status($target, $msg);
  if (($$details{'id_ata_feature_set_security_level'} eq 'high') || ($$details{'id_ata_feature_set_security_locked'} eq '1')) {
    $msg = loc("Device %1 is already locked. Attempting to proceed anyway.",$target);
    print $msg."\n";
    update_status($target, $msg);
  }
  # Set time estimate
  if ($$details{'id_ata_feature_set_security_erase_unit_min'} ne 0) {
    $PROGRESS{$target}{'finish'} = $$details{'id_ata_feature_set_security_erase_unit_min'};
  } else {
    $PROGRESS{$target}{'finish'} = 0;
  }
  # Write verification data to drive
  $msg = loc("Writing verification data to drive %1",$target);
  update_status($target, $msg);
  print "*** echo 'INCOMPLETE' | dd of=/dev/$target; sync;\n";
  my $result = '';
  if (!TEST_MODE) { $result = `echo 'INCOMPLETE' | dd of=/dev/$target 2>&1; sync;`; }
  $msg = loc("Failed to write to drive %1. Reset may fail, but proceeding anyway.",$target);
  if ($result =~ m/error|fail|problem|device/si) { message_box($msg, 'warning'); }
  sleep(1);
  # Set security password for the drive
  $msg = loc("Setting security password for device %1", $target);
  update_status($target, $msg);
  print "*** hdparm --user-master u --security-set-pass RESET /dev/$target\n";
  if (!TEST_MODE) { $result = `hdparm --user-master u --security-set-pass RESET /dev/$target 2>&1`; }
  $msg = loc("Failed to set password for drive %1",$target);
  if ($result =~ m/error|fail|problem|ioctl|invalid/si) { message_box($msg, 'warning'); }
  # Get the drive's current status
  $msg = loc("Refreshing status of device",$target);
  update_status($target, $msg);
  print "*** udevadm trigger\n";
  `udevadm trigger`;
  sleep(3);
  $details = get_drive_details($target);
  print Dumper($details);
  # Is drive password set?
  if (!TEST_MODE) {
    if (($$details{'id_ata_feature_set_security_enabled'} ne '1') || ($$details{'id_ata_feature_set_security_level'} ne 'high')) {
      $msg = loc("Could not enable security mode for device %1. This drive will not be reset.", $target);
      message_box($msg, 'error');
      $PROGRESS{$target}{'done'} = TRUE;
      $PROGRESS{$target}{'error'} = TRUE;
      $msg = loc("Unable to reset drive: Password could not be set");
      update_status($target, $msg);
      return 0;
    }
  }
  $msg = loc("Drive is ready to be reset");
  update_status($target, $msg);
  return 1;
}

sub reset_drive {
  # Safely issue ATA secure erase commands on a given device by
  # choosing which step to take based on its current status
  our %PROGRESS;
  my $target = $_[0];
  # Calculate time estimate
  $msg = loc("Calculating time estimate for completion");
  update_status($target, $msg);
  $PROGRESS{$target}{'start'} = time();
  if ($PROGRESS{$target}{'finish'} ne 0) {
    $PROGRESS{$target}{'finish'} = time() + $PROGRESS{$target}{'finish'} * 60;
  } else {
    $PROGRESS{$target}{'finish'} = 0;
  }
  # Start the reset process and set up a pipe
  $msg = loc("Preparing to issue secure erase command to device %1",$target);
  update_status($target, $msg);
  print "*** time hdparm --user-master u --security-erase RESET /dev/$target |\n";
  if (TEST_MODE) {
    # Pipe to a fake command just to demonstrate what it will look like
    open $PROGRESS{$target}{'pipe'}, "time sleep 15 |";
  } else {
    # Pipe to real ATA secure erase
    open $PROGRESS{$target}{'pipe'}, "time hdparm --user-master u --security-erase RESET /dev/$target |";
  }
  my $msg = loc("Reset started for %1, but we may not know when it finishes because we couldn't get flags on the process: ", $target);
  my $flags = fcntl($PROGRESS{$target}{'pipe'}, F_GETFL, 0) or message_box("$msg $!", 'warning');
  $flags |= O_NONBLOCK;
  $msg = loc("Reset started for %1, but we may not know when it finishes because we couldn't set flags on the process: ", $target);
  fcntl($PROGRESS{$target}{'pipe'}, F_SETFL, $flags) or message_box("$msg $!", 'warning');
  $msg = loc("Reset estimated to last from %1 until %2",$PROGRESS{$target}{'start'},$PROGRESS{$target}{'finish'});
  print "$msg...\n";
  $msg = loc("Resetting...");
  update_status($target, $msg);
  return 1;
}

sub update_status {
  # Change the status for a device and then update the window
  my $target = $_[0];
  my $status = $_[1];
  our %PROGRESS;
  $PROGRESS{$target}{'status'} = $status;
  update_progress();
}

sub update_progress {
  # Update the progress text and progress bar
  our %PROGRESS;
  our %drives;
  our @targets;
  if ($PROGRESS{'done'}) { return FALSE; }
  my $progress_bar = our $builder->get_object('progress_bar');
  my $tv = $builder->get_object('target_progress');
  my $device_heading = loc("Device");
  my $desc_heading = loc("Description");
  my $sn_heading = loc("Serial Number");
  my $drive_size_heading = loc("Drive Size");
  my $connection_heading = loc("Connection");
  my $estimate_heading = loc("Estimate");
  my $percent_heading = loc("Percent");
  my $status_heading = loc("Status");
  my $slist = Gtk2::SimpleList->new_from_treeview(
    $tv,
    $device_heading          => 'text',
    $desc_heading            => 'text',
    $sn_heading              => 'text',
    $drive_size_heading      => 'text',
    $connection_heading      => 'text',
    $estimate_heading        => 'text',
    $percent_heading         => 'text',
    $status_heading          => 'text',
  );
  foreach my $target (@targets) {
    my $dev_estimate = $drives{$target}{'id_ata_feature_set_security_erase_unit_min'};
    my $buffer = '';
    my $pct_complete = 'Unknown';
    if (defined($PROGRESS{$target}{'pipe'})) {
      my $rv = sysread($PROGRESS{$target}{'pipe'}, $buffer, 1);
      chomp($buffer);
      if ((!defined($rv))||($rv==1)) {
        # Show the time elasped and remaining
        if ((defined($dev_estimate)) && ($dev_estimate>0)) {
          my $est = $PROGRESS{$target}{'finish'} - $PROGRESS{$target}{'start'};
          my $cur = $PROGRESS{$target}{'finish'} - time();
          my $mins_left = sprintf("%.1f", $cur/60);
          my $pct = ($est-$cur)/$est;
          if ($mins_left<=0) { $mins_left = 0; $pct = 1; }
          my $time_remaining_msg = loc("About %1 minute", $mins_left);
          $time_remaining_msg .= "s" if ($mins_left!=1);
          $time_remaining_msg .= loc(" remaining");
          my $pct_text = sprintf("%.2f", $pct*100);
          $pct_complete = "$pct_text%";
          $PROGRESS{$target}{'status'} = $time_remaining_msg;
        } else {
          my $elapsed = time() - $PROGRESS{$target}{'start'};
          $elapsed = sprintf("%.1f", $elapsed/60);
          $msg = loc("Resetting: %1 minutes elapsed", $elapsed);
          $PROGRESS{$target}{'status'} = $msg;
        }
     } else {
        $msg = loc("Reset of %1 is complete.",$target);
        print "$msg.\n";
        if (!$PROGRESS{$target}{'done'}) {
          # This code will be executed once, when the target drive is done
          $PROGRESS{$target}{'finish'} = time();
          my $elapsed = $PROGRESS{$target}{'finish'} - $PROGRESS{$target}{'start'};
          $elapsed = sprintf("%.1f", $elapsed/60);
          $PROGRESS{$target}{'status'} = loc("Reset completed in %1 minutes", $elapsed);
          $pct_complete = "100%";
          print loc("Verifying no data is on drive...")."\n";
          my $bytes_checked = 0;
          $msg = loc("Could not read from device!");
          open(my $FILE, "/dev/$target") or print "$msg\n";
          binmode($FILE);
          while ( ($bytes_checked<=32768) && (read($FILE, my $byte, 1)) ) {
            $msg = loc("Verifying char %1 on %2: %3.",$bytes_checked,$target,ord($byte));
            print "$msg"."\n";
            if (ord($byte)!=0) {
              $msg = loc("Data found on drive!");
              print "*** $msg\n";
              $PROGRESS{$target}{'status'} = loc("Reset failed: Drive still contains data");
              $PROGRESS{$target}{'error'} = 1;
            }
            $bytes_checked++;
          }
          close($FILE);
          if ($bytes_checked==0) {
            $PROGRESS{$target}{'status'} = loc("Reset failed: Could not verify reset");
            $PROGRESS{$target}{'error'} = 1;
          }
        }
        $PROGRESS{$target}{'done'} = TRUE;
      }
    }
    if ((defined($dev_estimate)) && ($dev_estimate eq '0')) { $dev_estimate = loc('Unknown'); } else { $dev_estimate .= loc(" minutes"); }
    if ($PROGRESS{$target}{'done'}) { $pct_complete = 'DONE'; }
    if ($PROGRESS{$target}{'error'}) { $pct_complete = 'ERROR'; }
    push @{$slist->{data}}, [ $target, $drives{$target}{'dev_desc'}, $drives{$target}{'id_serial_short'}, $drives{$target}{'dev_size'}, $drives{$target}{'id_bus'}, $dev_estimate, $pct_complete, $PROGRESS{$target}{'status'} ];
    $slist->get_selection->unselect_all();
  }
  # Loop through all the targets to see if we're done
  $PROGRESS{'done'} = TRUE;
  foreach my $target (@targets) {
    if ($PROGRESS{$target}{'done'} ne TRUE) { $PROGRESS{'done'} = FALSE; }
  }
  # Set the progress bar
  if ($PROGRESS{'done'}) {
    # We are completely done with all drives
    refresh_window();
    final_cleanup();
    $progress_bar->set_fraction(1);
    $msg = loc("100% Complete");
    $progress_bar->set_text($msg);
    $msg = loc("Exit");
    $builder->get_object('cancel_button')->set_label($msg);
    refresh_window();
    beeper('done');
    $msg = loc("Drive reset complete. Restart your computer before writing any information to the target drives.");
    message_box($msg);
  } else {
    # Pulse the progress bar
    $msg = loc("Please wait...");
    $progress_bar->set_text($msg);
    $progress_bar->pulse();
  }
  refresh_window();
  print loc("Progress:");
  print Dumper(\%PROGRESS);
  if (!$PROGRESS{'done'}) { return TRUE; } else { return FALSE; }
}

sub final_cleanup {
  # Make sure all the drives are unlocked and no passwords are set
  our @targets;
  our %PROGRESS;
  print loc("Confirming that all drives are unlocked.")."\n";
  foreach my $target (@targets) {
    if ($PROGRESS{$target}{'error'}) {
      $msg = loc("Device %1 was not reset: %2",$target,$PROGRESS{$target}{'status'});
      message_box($msg, 'warning');
    }
    print loc("Checking device %1", $target)."\n";
    sleep(1);
    `udevadm trigger`;
    sleep(3);
    my $details = get_drive_details($target);
    print Dumper($details);
    # Is drive locked or password set?
    if (($$details{'id_ata_feature_set_security_level'} eq 'high') || ($$details{'id_ata_feature_set_security_locked'} eq '1')) {
      print loc("Reset completed on %1, but drive remains locked. Attempting to unlock drive...",$target)."\n";
      if (!TEST_MODE) { `hdparm --user-master u --security-disable RESET /dev/$target`; }
      # Get the drive's current status
      print loc("Refreshing status of device %1...",$target)."\n";
      print "*** udevadm trigger\n";
      `udevadm trigger`;
      sleep(3);
      $details = get_drive_details($target);
      # Is drive locked and password set?
      if (($$details{'id_ata_feature_set_security_level'} eq 'high') || ($$details{'id_ata_feature_set_security_locked'} eq '1')) {
        beeper('warning');
        $msg = loc("Could not disable security mode for device %1.  You may need to run the 'hdparm --security-disable' command before this drive can be used.", $target);
        message_box($msg, 'warning');
      }
    } else {
      print loc("Device %1 is unlocked.",$target)."\n";
    }
    # If the drive is unlocked, see if there are HPA sectors that could be freed
    if (($$details{'id_ata_feature_set_security_level'} ne 'high') && ($$details{'id_ata_feature_set_security_locked'} ne '1')) {
      # How many HPA sectors are in use?
      my $sectors_real = $$details{'hpa_sectors_real'};
      my $sectors_visible = $$details{'hpa_sectors_visible'};
      my $sector_diff = $sectors_real - $sectors_visible;
      if ($sector_diff > 0) {
        my $sector_mb = int($sector_diff/2048);
        print loc("Device %1 has %2 MB allocated to HPA.",$target,$sector_mb)."\n";
        my $result = '';
        $msg = loc("Would you like to attempt to free %1MB of host-protected area (HPA) on this hard drive?",$sector_mb);
        my $response = get_confirmation($msg);
        if (($response eq 'yes') && (!TEST_MODE)) {
          $result = `hdparm -N p$sectors_real /dev/$target 2>&1`;
          $msg = loc("Command executed. Response:\n\n%1",$result);
          message_box("Command executed. Response:\n\n$result");
        }
        $msg = loc("Would you like to attempt to restore the manufacturer's device configuration (DCO) on this hard drive?");
        $response = get_confirmation("$msg");
        if (($response eq 'yes') && (!TEST_MODE)) {
          $result = `hdparm --yes-i-know-what-i-am-doing --dco-restore /dev/$target 2>&1`;
          $msg = loc("Command executed. Response:\n\n%1",$result);
          message_box($msg);
        }
      }
    } else {
      print loc("Device %1 does not have any HPA space to recover.",$target)."\n";
    }
  }
}

sub next_tab {
  # Show the next tab of a Backup or Restore procedure
  our $builder;
  $builder->get_object('main_tabs')->next_page();
  my $tab = $builder->get_object('main_tabs')->get_current_page();
  reset_step($tab+1);
}

sub target_refresh {
  # Refresh and redo step 2
  reset_step(2);
}

sub get_target_list {
  # Return the partitions that have been selected from a list
  my @selected = ();
  my $tv = our $builder->get_object($_[0]);
  my $mdl = $tv->get_model();
  my $total = $mdl->iter_n_children();
  for (my $i=0; $i<$total; $i++) {
    my $iter = $mdl->get_iter_from_string("$i");
    my ($drive, $targeted) = $mdl->get($iter, 0, 1);
    if ($targeted eq '1') { push(@selected, $drive); }
  }
  return @selected;
}

sub target_changed {
  # The list of target drives has been clicked, and may have changed
  our %drives;
  our @targets;
  my $next_enabled = TRUE;
  @targets = get_target_list('target_drives');
  foreach my $dev (@targets) {
    if ((defined($drives{$dev}{'id_ata_feature_set_security_frozen'})) && ($drives{$dev}{'id_ata_feature_set_security_frozen'} eq '1')) {
      print loc("Drive %1 selected but currently frozen.",$dev)."\n";
      $next_enabled = FALSE;
    }
  }
  if (scalar(@targets)==0) { $next_enabled = FALSE; }
  our $builder->get_object('next_button')->set_sensitive($next_enabled);
}

sub set_subtitle {
  # Set the subtitle, which is a description of the current step
  my $subtitle = $_[0];
  our $builder->get_object('main_subtitle')->set_text($subtitle);
}

sub set_cursor {
  # Set the mouse pointer cursor
  my $cursor = $_[0];
  our $builder->get_object('main_app')->window()->set_cursor(Gtk2::Gdk::Cursor->new($cursor));
}

sub set_busy {
  # Disable the application to indicate that we are busy working
  if ($_[0] ne '') {
    set_cursor("watch");
    our $builder->get_object('main_app')->set_sensitive(FALSE);
    show_busy_bar($_[0]);
  } else {
    set_cursor("arrow");
    our $builder->get_object('main_app')->set_sensitive(TRUE);
    our $busy_bar->destroy();
  }
  refresh_window();
}

sub show_busy_bar {
  # Show a pulsing progress bar
  my $message = $_[0];
  my $please_wait_msg = loc("Please wait...");
  if (!defined($message)) { $message = $please_wait_msg; }
  my $parent = our $builder->get_object('main_app');
  our $busy_bar = Gtk2::Dialog->new($please_wait_msg, $parent,
    'destroy-with-parent',
    'gtk-cancel' => 'none'
  );
  my $pgb = Gtk2::ProgressBar->new();
  $pgb->set_text($message);
  my $abox = Gtk2::Alignment->new(.50, .50, 1, 1);
  $abox->set_padding(30, 30, 30, 30);
  $busy_bar->vbox->add($abox);
  $abox->add($pgb);
  $busy_bar->signal_connect(response => sub { $_[0]->destroy });
  $busy_bar->show_all;
  $busy_bar->window()->set_cursor(Gtk2::Gdk::Cursor->new('watch'));
  Glib::Timeout->add(100, \&update_busy_bar, $pgb);
}

sub update_busy_bar {
  # Update the pulsing progress bar
  my ($progress_bar) = @_;
  $progress_bar->pulse;
  return TRUE;
}

sub refresh_window {
  # Refresh the application window
  while (Gtk2->events_pending()) { Gtk2->main_iteration(); }
}

sub get_confirmation {
  # Get confirmation from a yes/no dialog
  our $builder;
  my $question = loc("Are you sure?");
  if (defined($_[0])) { $question = $_[0]; }
  my $dialog = Gtk2::MessageDialog->new($builder->get_object('main_app'),
    'destroy-with-parent',
    'warning', # message type
    'yes-no', # which set of buttons?
    $question);
  my $response = $dialog->run;
  $dialog->destroy;
  return $response;
}

sub message_box {
  # Show an informational $message of $type: [info|warning|question|error|other]
  my $parent = our $builder->get_object('main_app');
  my $message = $_[0];
  my $type = $_[1];
  if (!defined($type)) { $type = 'info'; }
  my $dialog = Gtk2::MessageDialog->new($parent, 'modal', $type, 'ok', $message);
  $dialog->run;
  $dialog->destroy;
}

sub set_drive_list {
  # Get all physical drives (not partitions)
  $msg = loc("Reading status for each drive...");
  set_busy($msg);
  our %drives = get_drive_list();
  our @targets;
  if (keys(%drives)==0) { fatal_crash('Could not locate any partitions on your computer.'); }
  my $tv = our $builder->get_object($_[0]);
  my $device_heading = loc("Device");
  my $reset_heading = loc("Reset?");
  my $desc_heading = loc("Description");
  my $sn_heading = loc("Serial Number");
  my $drive_size_heading = loc("Drive Size");
  my $connection_heading = loc("Connection");
  my $time_estimate_heading = loc("Time Estimate");
  my $status_heading = loc("Status");
  my $slist = Gtk2::SimpleList->new_from_treeview(
    $tv,
    $device_heading          => 'text',
    $reset_heading           => 'bool',
    $desc_heading            => 'text',
    $sn_heading              => 'text',
    $drive_size_heading      => 'text',
    $connection_heading      => 'text',
    $time_estimate_heading   => 'text',
    $status_heading          => 'text',
  );
  my $dev = '';
  for $dev (sort keys %drives) {
    my $id_bus = $drives{$dev}{'id_bus'};
    my $id_type = $drives{$dev}{'id_type'};
    my $dev_size = $drives{$dev}{'dev_size'};
    my $dev_desc = $drives{$dev}{'dev_desc'};
    my $dev_serial = $drives{$dev}{'id_serial_short'};
    my $dev_estimate = $drives{$dev}{'id_ata_feature_set_security_erase_unit_min'};
    my $unknown = loc("Unknown");
    my $minutes = loc(" minutes");
    if ((defined($dev_estimate)) && ($dev_estimate eq '0')) { $dev_estimate = $unknown; } else { $dev_estimate .= $minutes; }
    my $dev_status = $drives{$dev}{'id_ata_feature_set_security_frozen'};
    my $frozen = loc("FROZEN");
    my $ready = loc("READY");
    if ((defined($dev_status)) && ($dev_status eq '1')) { $dev_status = $frozen; } else { $dev_status = $ready; }
    if ((defined($drives{$dev}{'id_ata_feature_set_security_locked'})) && ($drives{$dev}{'id_ata_feature_set_security_locked'} eq '1')) { $dev_status = 'LOCKED'; }
    if ((defined($drives{$dev}{'id_ata_feature_set_security'})) && ($drives{$dev}{'id_ata_feature_set_security'} eq '1')) {
      # This drive is capable of being reset
      my $targeted = '0';
      if ($dev ~~ @targets) { $targeted = '1'; }
      push @{$slist->{data}}, [ $dev, $targeted, $dev_desc, $dev_serial, $dev_size, $id_bus, $dev_estimate, $dev_status ];
    }
  }
  $slist->get_selection->unselect_all();
  set_busy(FALSE);
  target_changed();
}

sub get_drive_list {
  # Get a list of all physical drives (and details for each)
  `udevadm trigger`;
  sleep(3);
  my %final_list = ();
  my @drives = `ls -1 /sys/block/ | grep -v -E '^fd|^loop|^ram'`;
  foreach my $dev (@drives) {
    chomp($dev);
    $final_list{$dev} = get_drive_details($dev);
  }
  #print Dumper(\%final_list);
  return %final_list;
}

sub get_drive_details {
  # Get the details for a given drive
  my $dev = $_[0];
  chomp($dev);
  my @data = `udevadm info --query=property --path=/sys/block/$dev | grep -E '^DEVNAME=|^ID_TYPE=|^ID_VENDOR=|^ID_MODEL=|^ID_SERIAL_SHORT=|^ID_BUS=|^ID_ATA_SATA=|^ID_ATA_FEATURE_SET_SECURITY|^ID_ATA_FEATURE_SET_HPA'`;
  my %details = ();
  foreach my $line (@data) {
    chomp($line);
    my ($key, $val) = ($line, $line);
    $key =~ s/=.*$//g;
    $val =~ s/^.*=//g;
    $key = lc($key);
    $details{$key} = $val;
  }
  my $dev_size = `fdisk -l /dev/$dev | grep -E '^Disk /dev/$dev' | sed 's/^.*: //' | sed 's/,.*\$//'`;
  chomp($dev_size);
  if ($dev_size eq '') { $dev_size = 'Unknown'; }
  $details{'dev_size'} = $dev_size;
  if (defined($details{'id_ata_sata'})) { $details{'id_bus'} = 's'.$details{'id_bus'}; }
  if (defined($details{'id_ata_feature_set_hpa'})) {
    if ($details{'id_ata_feature_set_hpa_enabled'} eq '1') {
      my $hpa_data = `hdparm -N /dev/$dev | grep 'HPA' | sed 's/^.*=\\s*//' | sed 's/,.*\$//'`;
      chomp($hpa_data);
      $hpa_data =~ s/^\s*|\s*$//g;
      my ($visible, $real) = split('/', $hpa_data);
      if (($visible > 10000) && ($real > 10000)) {
        $details{'hpa_sectors_real'} = $real;
        $details{'hpa_sectors_visible'} = $visible;
      }
    }
    my $dco_data = `hdparm --dco-identify /dev/$dev | grep 'Real max sectors' | wc -l`;
    chomp($dco_data);
    $details{'hpa_dco'} = $dco_data;
  }
  $details{'id_bus'} = uc($details{'id_bus'});
  $details{'dev_desc'} = $details{'id_model'};
  if (defined($details{'id_vendor'})) { $details{'dev_desc'} = $details{'id_vendor'}.' '.$details{'dev_desc'}; }
  $details{'dev_desc'} =~ s/_/ /g;
  $details{'dev_desc'} =~ s/^\s+|\s+$//g;
  if (TEST_MODE) { $details{'id_ata_feature_set_security_frozen'} = '0'; }
  return \%details;
}

sub get_drivenum_partnum {
  # Given a /dev/XXX partition, return the drive and partition numbers
  my $dpd = $_[0];
  $dpd =~ s/\/dev\/sd//g;
  my $dn = substr($dpd,0,1);
  $dn = ord("\L$dn")-96;
  my $pn = substr($dpd,1);
  return ($dn, $pn);
}

sub fatal_crash {
  # Crash out with a fatal error message
  my $parent = our $builder->get_object('main_app');
  my $message = $_[0].chr(13).chr(13).loc("Unable to continue. The program will now exit.");
  my $dialog = Gtk2::MessageDialog->new($parent, 'modal', 'error', 'cancel', $message);
  beeper('error');
  $dialog->run;
  $dialog->destroy;
  on_main_app_destroy();
}

sub error_message {
  # Show a non-fatal error message
  my $parent = our $builder->get_object('main_app');
  my $message = $_[0];
  my $dialog = Gtk2::MessageDialog->new($parent, 'modal', 'warning', 'ok', $message);
  beeper('warning');
  $dialog->run;
  $dialog->destroy;
}

sub on_main_app_destroy {
  # Close the application
  system('killall -9 hdparm');
  our $PROGRESS;
  if (defined($PROGRESS)) { close($PROGRESS); }
  Gtk2->main_quit();
  $msg = loc("Exiting");
  die("$msg.\n");
}

sub suspend_power {
  # Put the computer in standby/suspend mode
  `pm-suspend`;
}

sub trim {
  # Trim whitespace from both ends of a string
  my $string = shift;
  $string =~ s/^\s+//;
  $string =~ s/\s+$//;
  return $string;
}

sub beeper {
  # Play a system beep pattern
  my $tone = shift;
  my $args = '';
  if ($tone eq 'done') { $args = '-l 100 -f 1200 -n -l 100 -f 1800 -n -l 100 -f 2400'; }
  if ($tone eq 'warning') { $args = '-f 250 -r 3 -l 50'; }
  if ($tone eq 'error') { $args = '-l 1000 -f 100'; }
  system('beep '.$args);
  return 1;
}
